# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# oh-my-zsh specifics
export ZSH=$HOME/.oh-my-zsh
ZSH_THEME="agnoster"
if [[ "$(uname -s)" == "Darwin" ]]
then
	plugins=(brew bundler gem gpg-agent git knife mvn node npm osx ruby rvm sublime sudo terminalapp vagrant vim)
else
	plugins=(bundler gem git knife mvn node npm ruby rvm sudo vagrant vim)
fi
source $ZSH/oh-my-zsh.sh
export PATH=$HOME/.local/bin:/usr/local/bin:/opt/chefdk/bin:$PATH

#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="/Users/fran/.sdkman"
[[ -s "/Users/fran/.sdkman/bin/sdkman-init.sh" ]] && source "/Users/fran/.sdkman/bin/sdkman-init.sh"

# added by travis gem
[ -f /Users/fran/.travis/travis.sh ] && source /Users/fran/.travis/travis.sh

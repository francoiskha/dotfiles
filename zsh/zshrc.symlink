# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a $HOME/.localrc ]]
then
  source $HOME/.localrc
fi

unset config_files
# all of our zsh files
typeset -U config_files
config_files=($HOME/.local/.dotfiles/**/*.zsh(N) $DOTFILES/**/*.zsh(N))

# load the aliases files
for file in ${(M)config_files:#*/aliases.zsh}
do
	source $file
done

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
	source $file
done

# load everything but the path, completion and aliased files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/aliases.zsh}
do
	source $file
done

if [[ "$config_files_path" == "$DOTFILES" ]] 
	then
	# initialize autocomplete here, otherwise functions won't be loaded
	autoload -U compinit
	compinit
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
	source $file
done

unset config_files